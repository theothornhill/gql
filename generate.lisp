(in-package :gql)

(defun add-indent (level)
  "Add indentation for pretty printing.

(add-indent 0) => \"\"
(add-indent 1) => \"  \"
(add-indent 2) => \"    \"
"
  (let ((indentation ""))
    (dotimes (_ level)
      (setf indentation (cat indentation "  ")))
    indentation))

(defun unfold-nodes (stream nodes)
  "Splices open a list of nodes, displaying them without the enclosing ( )."
  (format stream "狺铒溴螬ㄤ彐躅玑翳弪铒溴铒溴扉篝轭溴铘戾鲥飑⒚镬戾泗扉篝镦骘蝽狒翦篚忸镤弩婶轭溴铘麒弪铄沐篌狎鏖翳桢祓骝镯汜祆麸桥闻伊耘翳孱蝈趱蝾扉篝镦篝蜷铉螽磲疸狎灬礅溽铒溴ㄦ矧磲铋幄ㄧ孱弪狒铒溴轭溴铘戾鲥飑┅铒溴扉篝┅ㄤ彐珏铄蜷珏铄蜥翦铒溴镳糸镱犰轭溴铘戾鲥篝蝈犴ê滹沲礤铘狒轱痱轭铒溴狍鲠扉球耩柩篝狒屙孱舢┅ㄤ彐礤翳镤珏铄蜥翦è铒溴滹沲礤铘镳糸镱犰ㄩ钿孱舡戾鲥癌篝蝈犴舂躅骘熹铒溴篝蝈犴ㄧ狒桢颦铒溴ㄤ彐轭轸轱铙铒溴轭溴铘戾鲥飑┅ㄤ彐礤翳镤珏铄蜥翦è铒溴屮邈豸徕戾溴骈铋糸镱镳糸镱犰ㄩ钿孱舡戾鲥癌篝蝈犴舂躅骘熹铒溴篝蝈犴ㄧ狒桢颦铒溴ㄤ彐轭轸轱铙铒溴轭溴铘戾鲥飑┅ㄤ彐礤翳镤珏铄蜥翦è铒溴豉疱簌篝屙溴骈铋糸镱镳糸镱犰ㄩ钿孱舡戾鲥癌篝蝈犴舂躅骘熹铒溴篝蝈犴ㄧ狒桢颦铒溴ㄤ彐轭轸轱铙铒溴轭溴铘戾鲥飑┅ㄤ彐礤翳镤珏铄蜥翦è铒溴豉疱簌篝屙屮翦铙轱瞟镳糸镱犰ㄩ钿孱舡戾鲥癌篝蝈犴舂躅骘熹铒溴篝蝈犴ㄧ狒桢颦铒溴ㄤ彐轭轸轱铙铒溴轭溴铘戾鲥飑┅ㄤ彐礤翳镤珏铄蜥翦è铒溴镳弪狒轱瞽溴骈铋糸镱镳糸镱犰ㄩ钿孱舡戾鲥癌篝蝈犴铋飑ㄦ矧磲篝蝈犴狺累狺蔺累狺幄镳弪狒轱铒溴麒孱钺礤铒溴ㄧ孱弪狒钺礤铒溴┅换鲠蜷徕戾溴骈铋糸镱铒溴ㄧ狒桢颦铒溴ㄤ轵邈糸鲥铒溴轭溴铘戾鲥飑ㄧ孱弪狒箦戾泗轱瞽箦铒溴ū轭溴铘戾鲥飑换麒孱ㄤ彐轭轸轱铙铒溴ㄧ狒桢颦铒溴ㄤ彐轭轸轱铙铒溴轭溴铘戾鲥飑┅ㄤ彐礤翳镤珏铄蜥翦è铒溴骝徵礤铘溴骈铋糸镱镳糸镱犰ㄩ钿孱舡戾鲥癌篝蝈犴舂躅骘熹铒溴篝蝈犴ㄧ狒桢颦铒溴ㄤ彐轭轸轱铙铒溴轭溴铘戾鲥飑┅ㄤ彐礤翳镤珏铄蜥翦è铒溴箦戾泗轱瞽箦舂镳糸镱犰ㄩ钿孱舡戾鲥癌篝蝈犴铋飑换郁狎怡徜溟铉换五黛轭换疹骘熹翳箦戾泗轱瞽箦换腻溴铘换五黛轭ㄦ矧磲篝蝈犴ㄣ狒Ⅺ狺猃ㄧ狒桢颦铒溴箦戾泗轱铙铒溴轭溴铘戾鲥飑ㄡ滗轭溴铘ū轭溴铘戾鲥飑┅ㄤ彐礤翳镤珏铄蜥翦è铒溴狎珲礤铘镳糸镱犰ㄩ钿孱舡戾鲥癌篝蝈犴铋飑ㄦ矧磲篝蝈犴岷幄ㄧ孱弪狒钺礤铒溴轭溴铘戾鲥飑ㄧ孱弪狒鲠祯铒溴轭溴铘戾鲥飑┅ㄤ彐礤翳镤珏铄蜥翦è铒溴骈屐洎镳糸镱犰ㄩ钿孱舡戾鲥癌篝蝈犴铋飑换龄鲠钽邃屮犴痨搴换箜犰煨殂痱镦殪逍殂箝搴洞箝宀辈俯荔腴皎殒趄蹂ㄦ矧磲篝蝈犴ㄣ狒幄换轭溴铘累岷茛换犰獒累狺茛换钺礤累狺蕃)]"          ;; arguments, comma separated
                      "@[ 狺]"            ;; directives
                      "@[ a]")                  ;; selection-set
          (add-indent indent-level)
          (when (alias node) (generate (alias node) indent-level))
          (generate (name node))
          (when (arguments node)
            (gather-nodes (arguments node) indent-level))
          (gather-nodes (directives node) indent-level)
          (when (selection-set node)
            (generate (selection-set node) (1+ indent-level)))))

(defmethod generate ((node name) &optional (indent-level 0) (stream nil))
  (declare (ignorable indent-level))
  (format stream "@[a]" (name node)))

(defmethod generate ((node directive) &optional (indent-level 0) (stream nil))
  (declare (ignorable indent-level))
  (format stream (cat "@"
                      "@[a]"
                      "@[(狺蕃)]"          ;; arguments, comma separated
                      )
          (generate (name node))
          (when (arguments node)
            (gather-nodes (arguments node) indent-level))))

;;; Values
(defmethod generate ((node int-value) &optional (indent-level 0) (stream nil))
  (declare (ignorable indent-level))
  (format stream "@[a]" (value node)))

(defmethod generate ((node boolean-value) &optional (indent-level 0) (stream nil))
  (declare (ignorable indent-level))
  (let ((bool (if (value node) "true" "false")))
    (format stream "@[a]" bool)))

(defmethod generate ((node var) &optional (indent-level 0) (stream nil))
  (declare (ignorable indent-level))
  (format stream "@[$a]" (generate (name node))))
